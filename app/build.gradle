apply plugin: 'com.android.application'

/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Fetch build number from command line arguments (for CI)
ext.build_number = project.hasProperty('BUILD_NUMBER') ? project.getProperty('BUILD_NUMBER') : "1"
ext.build_label = project.hasProperty('BUILD_LABEL') ? project.getProperty('BUILD_LABEL') : "SNAPSHOT"
ext.logsource_id = project.hasProperty('LOGSOURCE_ID') ? project.getProperty('LOGSOURCE_ID') : ""

apply from: 'jacoco.gradle'
apply from: 'enexpress.gradle'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'

// Disable unwanted tasks (if any) - see enexpress.gradle
project.tasks.all { task -> 
    if (project.excludedTasks.contains(task.name)) {
        println "Disabling task " + task.name
        task.enabled = false
    }
}

android {
    compileSdkVersion 30
    ndkVersion "21.3.6528147"
    defaultConfig {
        applicationId 'com.google.android.apps.exposurenotification'

        minSdkVersion 21
        targetSdkVersion 30

        versionCode build_number.toInteger()
        versionName build_label

        // The value of 'appLinkHost' is placeholder. It is to be replaced with 'appLinkHost' field
        // in the config.
        manifestPlaceholders = [appLinkHost: "applink.host"]

        // This is currently unused, but necessary if we add instrumented tests in the future
        testInstrumentationRunner "com.google.android.apps.exposurenotification.CustomTestRunner"

        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }

        ndk {
            debugSymbolLevel = 'FULL'
        }

        buildConfigField("String", "LOGSOURCE_ID", "\"${logsource_id}\"")
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
            versionNameSuffix ".debug"
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        debug {
            proto {
                srcDirs 'src/main/java/com/google/android/apps/exposurenotification/proto'
            }
            assets {
                srcDirs += files("$projectDir/schemas".toString())
            }
        }
        release {
            proto {
                srcDir 'src/main/java/com/google/android/apps/exposurenotification/proto'
            }
        }
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }

    lintOptions {
        abortOnError false
    }
    buildToolsVersion '30.0.1'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

hilt {
    enableTransformForLocalTests = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    if (!file('libs/prioclient-release.aar').exists()) {
      implementation project(':prioclient')
    }

    annotationProcessor 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    annotationProcessor 'com.google.auto.value:auto-value:1.7.3'
    annotationProcessor 'com.google.dagger:hilt-android-compiler:2.28.3-alpha'

    debugImplementation 'androidx.fragment:fragment-testing:1.2.5'

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.concurrent:concurrent-futures:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    implementation 'androidx.hilt:hilt-work:1.0.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.room:room-guava:2.2.5'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.work:work-runtime:2.5.0-beta01'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.gms:play-services-base:17.4.0'
    implementation 'com.google.android.gms:play-services-basement:17.4.0'
    implementation 'com.google.android.gms:play-services-tasks:17.2.0'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'com.google.auto.value:auto-value-annotations:1.7.3'
    implementation "com.google.dagger:hilt-android:2.28.3-alpha"
    implementation 'com.google.firebase:firebase-firestore:21.6.0'
    implementation 'com.google.firebase:firebase-datatransport:17.0.8'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.protobuf:protobuf-javalite:3.11.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'
    implementation "com.mikepenz:aboutlibraries:8.5.0"

    // Debug-only libraries
    debugImplementation 'com.google.zxing:core:3.3.0'
    debugImplementation('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }

    // JVM test dependencies
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'org.mockito:mockito-inline:2.25.0'
    //noinspection FragmentGradleConfiguration
    testImplementation 'androidx.fragment:fragment-testing:1.2.5'
    testImplementation 'androidx.room:room-testing:2.2.5'
    testImplementation 'androidx.test.ext:junit:1.1.2-rc01'
    testImplementation 'androidx.test:core:1.3.0-rc01'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.google.guava:guava-testlib:29.0-jre'
    testImplementation 'com.google.dagger:hilt-android-testing:2.28.3-alpha'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'commons-io:commons-io:2.6'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.28.3-alpha'

    // Instrumented test dependencies
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-rc01'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.28.3-alpha'
    androidTestAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.28.3-alpha'
}

aboutLibraries {
    configPath = "app/aboutlibraries"
}

configurations {
    all {
        exclude module: 'httpclient'
    }
}
